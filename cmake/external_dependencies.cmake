include(FetchContent)

OPTION(FETCH_DEPENDENCIES_WITH_CMAKE "Fetch dependencies with CMake: Can be OFF, ON, or MISSING. If the latter, CMake will download only dependencies which are not previously found." OFF)
MESSAGE(STATUS "ðŸ”§ FETCH_DEPENDENCIES_WITH_CMAKE: ${FETCH_DEPENDENCIES_WITH_CMAKE}")

if(FETCH_DEPENDENCIES_WITH_CMAKE STREQUAL "OFF")
    set(FIND_PACKAGE_OPTIONS REQUIRED)
else()
    set(FIND_PACKAGE_OPTIONS QUIET)
endif()

function(find_package_or_fetch)
    set(options)
    set(oneValueArgs CONAN_PKG_NAME PACKAGE_NAME GIT_REPOSITORY TAG)
    set(multiValueArgs)
    cmake_parse_arguments(PARSE_ARGV 0 arg
        "${options}" "${oneValueArgs}" "${multiValueArgs}"
    )

    set(actual_pkg_name ${arg_PACKAGE_NAME})
    if(arg_CONAN_PKG_NAME)
        set(actual_pkg_name ${arg_CONAN_PKG_NAME})
    endif()

    if(NOT FETCH_DEPENDENCIES_WITH_CMAKE STREQUAL "ON")
        find_package(${actual_pkg_name} ${FIND_PACKAGE_OPTIONS})
    endif()

    if(FETCH_DEPENDENCIES_WITH_CMAKE STREQUAL "ON" OR FETCH_DEPENDENCIES_WITH_CMAKE STREQUAL "MISSING")
        if(NOT ${actual_pkg_name}_FOUND)
            message(STATUS "ðŸ“¦ Fetching ${arg_PACKAGE_NAME}")
            FetchContent_Declare(
                ${arg_PACKAGE_NAME}
                GIT_SHALLOW TRUE
                GIT_REPOSITORY ${arg_GIT_REPOSITORY}
                GIT_TAG ${arg_TAG}
                GIT_PROGRESS TRUE
                SYSTEM
                EXCLUDE_FROM_ALL)
            FetchContent_MakeAvailable(${arg_PACKAGE_NAME})
            message(STATUS "\tâœ… Fetched ${arg_PACKAGE_NAME}")
        else()
            message(STATUS "ðŸ“¦ ${actual_pkg_name} found here: ${${actual_pkg_name}_DIR}")
        endif()
    endif()
endfunction()

set(SPARROW_BUILD_SHARED ${SPARROW_PYCAPSULE_BUILD_SHARED})
find_package_or_fetch(
    PACKAGE_NAME sparrow
    GIT_REPOSITORY https://github.com/man-group/sparrow.git
    TAG 1.3.0
)

if(NOT TARGET sparrow::sparrow)
    add_library(sparrow::sparrow ALIAS sparrow)
endif()

if(SPARROW_PYCAPSULE_BUILD_TESTS)
    find_package_or_fetch(
        PACKAGE_NAME doctest
        GIT_REPOSITORY https://github.com/doctest/doctest.git
        TAG v2.4.12
    )
endif()

find_package(Python REQUIRED COMPONENTS Interpreter Development.Module Development.Embed)

execute_process(
    COMMAND "${Python_EXECUTABLE}" -m nanobind --cmake_dir
    OUTPUT_STRIP_TRAILING_WHITESPACE
    OUTPUT_VARIABLE nanobind_ROOT
    RESULT_VARIABLE nanobind_query_result
    ERROR_VARIABLE nanobind_query_error
)

if(NOT nanobind_query_result STREQUAL "0" OR nanobind_ROOT STREQUAL "")
    message(WARNING
        "Failed to detect nanobind CMake directory via 'python -m nanobind --cmake_dir' "
        "(result='${nanobind_query_result}', error='${nanobind_query_error}'). "
        "Continuing without nanobind_ROOT; CMake will rely on standard find_package/fetch.")
    unset(nanobind_ROOT)
endif()
find_package_or_fetch(
    PACKAGE_NAME nanobind
    GIT_REPOSITORY https://github.com/wjakob/nanobind.git
    TAG v2.9.2
)

if(TARGET nanobind)
    nanobind_build_library(nanobind-static)
endif()
